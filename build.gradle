plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			srcDir file('src/integrationTest/java')
		}
		resources {
			srcDir file('src/integrationTest/resources')
		}
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	useJUnitPlatform {
		includeEngines 'cucumber'
	}
	testLogging {
		events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
		showStandardStreams = true
	}
}

tasks.withType(ProcessResources).configureEach {
	if (name == "processIntegrationTestResources") {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.postgresql:postgresql:42.7.5'
	implementation platform('software.amazon.awssdk:bom:2.31.26')
	implementation 'software.amazon.awssdk:sqs'
	implementation 'software.amazon.awssdk:regions'
	implementation 'software.amazon.awssdk:auth'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.mockito:mockito-inline:5.2.0"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'org.junit.platform:junit-platform-suite-api'
	integrationTestImplementation 'io.cucumber:cucumber-java:7.14.0'
	integrationTestImplementation 'io.cucumber:cucumber-spring:7.14.0'
	integrationTestImplementation 'io.cucumber:cucumber-junit-platform-engine:7.14.0'
	integrationTestImplementation 'org.junit.platform:junit-platform-suite-api:1.10.1'
}

afterEvaluate {
	sourceSets.integrationTest.compileClasspath += sourceSets.main.output
	sourceSets.integrationTest.runtimeClasspath += sourceSets.main.output
}

gradle.projectsEvaluated {
	println "integrationTest.java.srcDirs: " + sourceSets.integrationTest.java.srcDirs
	println "integrationTest.resources.srcDirs: " + sourceSets.integrationTest.resources.srcDirs
}

